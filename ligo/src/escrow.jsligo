#import "types.jsligo" "Types"
#import "admin.jsligo" "Admin"

type escrow_storage = Types.escrow_storage;

type withdraw_param = {
  to: address,
  amount: tez,
};

type escrow_param =
    ["Admin", Admin.simple_admin]
  | ["Deposit"]
  | ["Withdraw", withdraw_param];

type return_type = [list<operation>, escrow_storage];

const empty_operation_list = (list([]) as list<operation>);

const deposit = (storage: escrow_storage) : return_type => {
  const amount = Tezos.get_amount();
  if (amount <= (0 as tez)) {
    return failwith("INVALID_TEZOS_AMOUNT");
  }
  return [empty_operation_list, storage];
}

const withdraw = ([param, storage]: [withdraw_param, escrow_storage]) : return_type => {
  const escrow = Tezos.get_balance();
  if (escrow <= param.amount) {
    return failwith("INSUFFICIENT_ESCROW");
  }

  const receiver = Tezos.get_contract_with_error(param.to, "INVALID_ADDRESS") as contract<unit>;
  const op = Tezos.transaction(unit, param.amount, receiver);

  return [list([op]), storage];
}

export const main = ([param, storage] : [escrow_param, escrow_storage]) : return_type => {
  match(param, {
    Admin: (p: Admin.simple_admin) => {
      return Admin.main(p, storage);
    },
    Deposit: () => {
      return deposit(storage);
    },
    Withdraw: (p: withdraw_param) => {
      Admin.fail_if_not_admin(storage);
      return withdraw(p, storage);
    },
  });
};
